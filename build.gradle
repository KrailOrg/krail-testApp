buildscript {
    ext.kotlin_version = '1.1.4-2'
    ext.kaytee_plugin_version = "0.18.2.0"
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "uk.q3c.kaytee:kaytee-plugin:$kaytee_plugin_version"
    }
}

class Pause extends DefaultTask {
    int delaySecs = 5

    @TaskAction
    void pause() {

        int i = delaySecs
        while (i >= 1) {
            println "${i} seconds left"
            i--
            Thread.sleep(1000)
        }
    }
}

plugins {
    id "com.devsoap.plugin.vaadin" version "1.2.0"
}

apply plugin: 'uk.q3c.kaytee'
apply plugin: "kotlin"

description = 'Functional test application for Krail and its additional modules'

group = 'uk.q3c.krail'


repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {

    //Krail
    compile 'uk.q3c.krail:krail-jpa:0.10.1.0'
    compile 'uk.q3c.krail:krail:0.10.2.0'

    //UI components
    compile 'de.steinwedel.vaadin.addon:messagebox:2.0.7'
    compile 'org.vaadin.addons:stepper:2.2.1'
    // Missing from stepper
    compile 'org.json:json:20141113'
    //vaadin push
//    compile 'com.vaadin:vaadin-push:' + vaadinVersion

    testCompile 'junit:junit:4.12'
    testCompile 'uk.q3c.krail:krail-bench:0.10.0.0.4a79200'
//    testCompile project(':krail-orient')


}

vaadin{
    version = '7.7.10'
    logToConsole = true
}

ext.warName = (project.name.toLowerCase() + '.war')
war {
    exclude 'WEB-INF/lib/*'
    archiveName = warName
}

task copyWar(type: Copy) {
    dependsOn war
    File userHome = new File(System.getProperty('user.home'))
    File source = new File(project.buildDir, "libs")
    File dest = new File(userHome, "tomcat8.5/webapps")
    println ">>>>>>>>>>>>>>>>>>>>>>>>>>> from: ${source} to ${dest}"
    from source
    into dest
}

task pauseForApp(type: Pause){
    delaySecs =20
    dependsOn copyWar
}



test.dependsOn pauseForApp


vaadinRun {
    serverPort = 8001
    server = 'jetty'
    debug false
}

kaytee {
    version{
        number =  "0.10.3.0"
    }
    release{
        toBintray = false
    }
}



bintray {
    user = 'dsowerby'
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

