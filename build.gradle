buildscript {
    ext.kotlin_version = '1.2.20'
    ext.kaytee_plugin_version = "0.18.10.0"
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "uk.q3c.kaytee:kaytee-plugin:$kaytee_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

class Pause extends DefaultTask {
    int delaySecs = 5

    @TaskAction
    void pause() {

        int i = delaySecs
        while (i >= 1) {
            println "${i} seconds left"
            i--
            Thread.sleep(1000)
        }
    }
}

plugins {
    id "com.devsoap.plugin.vaadin" version "1.3.0"
}

apply plugin: 'uk.q3c.kaytee'
apply plugin: 'kotlin'

description = 'Functional test application for Krail and its additional modules'

group = 'uk.q3c.krail'

ext.assertjVersion = '3.8.0'
ext.vaadinTestbenchVersion = '5.0.0'
ext.vaadinVersion='8.2.0'
ext.gsonVersion = '2.3.1'

kaytee {
    version {
        number = "0.14.0.0"
    }
    release {
        mergeToMaster = false
        toBintray = false
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

// guava 23.0 causes an error
// java.lang.IllegalAccessError: tried to access method com.google.common.util.concurrent.SimpleTimeLimiter
configurations.all {
    resolutionStrategy {
        force 'com.google.guava:guava:22.0'
        // GWT requires an old version of the validation API.  Changing to a newer version breaks widgetset compile but throws no errors
        force 'javax.validation:validation-api:1.0.0.GA'
    }
}

dependencies {

    // Kotlin

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    //Krail
    compile 'uk.q3c.krail:krail-jpa:0.14.0.0'
    compile 'uk.q3c.krail:krail:0.14.0.0'
    compile 'uk.q3c.krail:eventbus-mbassador:0.5.0.0' // for Handler / Listener annotations
//   compile group: 'com.vaadin', name: 'vaadin-compatibility-client', version: '8.1.3'
//   compile group: 'com.vaadin', name: 'vaadin-compatibility-themes', version: '8.1.3'

    //assert
    compile 'org.assertj:assertj-core:' + assertjVersion

    // html parser
    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.3'



    compile 'uk.q3c.krail:krail:0.14.0.0' // needed for ID class

    //Testbench
    compile "com.vaadin:vaadin-testbench:$vaadinTestbenchVersion"
    compile "com.vaadin:vaadin-testbench-api:$vaadinVersion"  // get an out of date API without this

    //UI components
    compile 'de.steinwedel.vaadin.addon:messagebox:2.0.7'
    compile 'org.vaadin.addons:stepper:2.4.0'
    // Missing from stepper
    compile 'org.json:json:20141113'
    //vaadin push
//    compile 'com.vaadin:vaadin-push:' + vaadinVersion

    testCompile "org.amshove.kluent:kluent:1.34"
    testCompile 'com.codeborne:selenide:4.9.1'
    testCompile 'junit:junit:4.12'
//    testCompile('uk.q3c.krail:krail-bench:0.14.0.0') {
//        exclude group: 'uk.q3c.krail', module: 'krail'
//    }

}
//    testCompile project(':krail-orient')


vaadin {
    version = '8.2.0'
    logToConsole = true
}

vaadinCompile{
    manageWidgetset false
}

ext.warName = (project.name.toLowerCase() + '.war')
war {
    exclude 'WEB-INF/lib/*'
    archiveName = warName
}

task copyWar(type: Copy) {
    dependsOn war
    File userHome = new File(System.getProperty('user.home'))
    File source = new File(project.buildDir, "libs")
    File dest = new File(userHome, "tomcat8.5/webapps")
    println ">>>>>>>>>>>>>>>>>>>>>>>>>>> from: ${source} to ${dest}"
    from source
    into dest
}

task pauseForApp(type: Pause) {
    delaySecs = 20
    dependsOn copyWar
}



test.dependsOn pauseForApp


vaadinRun {
    serverPort = 8001
    server = 'jetty'
    debug false
}


